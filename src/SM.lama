-- Stack machine.

import Array;
import List;
import Fun;
import Collection;
import World;
import State;
import Expr;
import Buffer;
import Fun;

-- Stack code printer. Takes a list of SM instructions, return its
-- string representation.
public fun showSMInsn (i) {
  case i of
    READ         -> "READ"
  | WRITE        -> "WRITE"
  | BINOP (s)    -> sprintf ("BINOP %s", s)
  | LD    (x)    -> sprintf ("LD %s", x)
  | LDA   (x)    -> sprintf ("LDA %s", x)
  | ST    (x)    -> sprintf ("ST %s", x)
  | STI          -> "STI"
  | CONST (n)    -> sprintf ("CONST %d", n)
  | LABEL (s)    -> sprintf ("LABEL %s", s)
  | JMP   (l)    -> sprintf ("JMP %s", l)
  | CJMP  (c, l) -> sprintf ("CJMP %s, %s", c, l)
  | DUP          -> "DUP"
  | DROP         -> "DROP"
  esac
}

public fun showSM (prg) {
  map (fun (i) {showSMInsn (i) ++ "\n"}, prg).stringcat
}

-- Evaluation environment: keeps a mapping between labels and (sub)programs
fun initEvalEnv (insns) {
  var map =
    fix (fun (rec) {
          fun ([m, insns]) {
            case insns of
              {}               -> m
            | LABEL (lab) : tl -> rec ([addMap (m, lab, insns), tl])
            | _ : tl           -> rec ([m, tl])
            esac
          }
        }) $ [emptyMap (compare), insns];
  
  [fun (l) {
     case findMap (map, l) of Some (insns) -> insns esac
  }]
}

-- Accessor function
fun fromLabel (env, lab) {
  env [0] (lab)
}

-- Stack machine interpreter. Takes an environment, an SM-configuration and a program,

-- Stack machine interpreter. Takes an SM-configuration and a program,
-- returns a final configuration
fun eval (env, c, insns) {

  fun evalInstruction (c, ins) {
    var stack = c[0], st = c[1], w = c[2];

    case ins of
      READ      -> (
        var readedWorld = w.readWorld;
        var value = readedWorld.fst, newWorld = readedWorld.snd;
        [Value (value) : stack, st, newWorld]
      )
    | WRITE     -> (
        var packedValue = stack.hd, stackTail = stack.tl;
        case packedValue of
            Value (value) -> [stackTail, st, writeWorld(value, w)]
            | _ -> failure ("Should not reach")
        esac
      )
    | BINOP (binop) -> (
        var packedRarg = stack.hd, ytail = stack.tl;
        var packedLarg = ytail.hd, stackTail = ytail.tl;
        case packedLarg of
            Value (larg) -> case packedRarg of
                Value (rarg) -> (
                    var value = evalOp (binop, larg, rarg);
                    [Value (value) : stackTail, st, w]
                )
                | _ -> failure ("Should not reach")
            esac
            | _ -> failure ("Should not reach")
        esac
      )
    | LD    (x) -> [Value (st (x)) : stack, st, w] -- FIXME for undefined "x"s returns Some (0)
    | LDA   (x) -> [Ref (x) : stack, st, w]
    | ST    (x) -> (
        var packedValue = stack.hd;
        case packedValue of
            Value (value) -> [stack, st <- [x, value], w]
            | _ -> failure ("Should not reach")
        esac
    )
    | STI       -> (
        var packedValue = stack.hd, ytail = stack.tl;
        var packedRef = ytail.hd, stackTail = ytail.tl;
        case packedRef of
             Ref (x) -> case packedValue of
                 Value (v) -> (
                     [Value (v) : stackTail, st <- [x, v], w]
                 )
                 | _ -> failure ("Should not reach")
            esac
            | _ -> failure ("Should not reach")
        esac
    )
    | CONST (n) -> [Value (n) : stack, st, w]
    | DROP      -> [stack.tl, st, w]
    | DUP       -> [stack.hd : stack, st, w]
    esac
  }

  case insns of
      ins : insns ->
        case ins of
          LABEL (_) -> eval (env, c, insns)
        | JMP (lab) -> eval (env, c, env.fromLabel (lab))
        | CJMP (cond, lab) -> (
           var stack = c[0], st = c[1], w = c[2];
           case stack of
              x : stack -> eval (env, [stack, st, w], if (case cond of "nz" -> x | "z" -> x == 0 esac) then env.fromLabel (lab) else insns fi)
           esac
        )
        | _ -> eval (env, evalInstruction(c, ins), insns)
        esac
    | {  } -> c
  esac
}

-- Runs a stack machine for a given input and a given program, returns an output
public fun evalSM (input, insns) {
  eval (initEvalEnv (insns), [{}, emptyState, createWorld (input)], insns)[2].getOutput
}

-- Compilation environment: generates labels
fun makeCompEnv (n) {
  [fun () {
     [sprintf ("L%d", n), makeCompEnv (n+1)]
  }]
}

fun initCompEnv () {
  makeCompEnv (0)
}

-- Accessor function: generates one label
fun genLabel (env) {
  env [0] ()
}

-- Utility function: generates n labels
fun genLabels (env, n) {
  fun inner (env, n) {
    if n == 0
    then {env}
    else case env.genLabel of
           [lab, env] -> lab : inner (env, n-1)
         esac
    fi
  }

  listArray (inner (env, n))
}

-- Compiles an expression into a stack machine code.
-- Takes an expression, returns a list of stack machine instructions
fun compileExpr (expr) {

  fun compileExprHelper(expr) {
    case expr of
        Const (n)                   -> { CONST (n) }
      | Var (name)                  -> { LD (name) }
      | Binop (binop, lexpr, rexpr) -> compileExprHelper (lexpr) +++ compileExprHelper (rexpr) +++ { BINOP (binop) }
    esac
  }

  listBuffer(compileExprHelper(expr))
}

-- Compiles a statement into a stack machine code.
-- Takes a statement, returns a list of stack machine
-- instructions.
public fun compileSM (stmt) {
  fun label (lab, labUsed) {
    if labUsed
    then singletonBuffer (LABEL (lab))
    else emptyBuffer ()
    fi
  }

  fun getCodeFromCompile(r@[labUsed, env, codeBuffer]) {
    codeBuffer
  }

  -- Generates SM code for a given statement
  --    lab  --- a label th go to after the statement is executed
  --    env  --- compilation environment
  --    expr --- a expression to generate SM code for
  -- Returns a triple:
  --    boolean flag indicating if "lab" was used
  --    updated environment
  --    code buffer
  fun compile (lab, env, expr) {
    case expr of
      Skip                        -> [false, env, emptyBuffer ()]
    | Const (n)                   -> [false, env, singletonBuffer (CONST (n))]
    | Var (name)                  -> [false, env, singletonBuffer (LD (name))]
    | Binop (binop, lexpr, rexpr) -> (
        case env.genLabels(2) of
            [lab1, lab2, env] -> case compile (lab1, env, lexpr) of
                [lab1Used, env, code1] -> case compile (lab2, env, rexpr) of
                    [lab2Used, env, code2] -> [false, env, code1 <+> label (lab1, lab1Used) <+> code2 <+> label (lab2, lab2Used) <+ BINOP (binop)]
                esac
            esac
        esac
    )
    | Ref (x)        -> [false, env, singletonBuffer (LDA (x))]
    | Ignore (e)     -> (
        case env.genLabel of
            [lab, env] -> case compile (lab, env, e) of
                [labUsed, env, code] -> [false, env, code <+> label (lab, labUsed) <+ DROP]
            esac
        esac
    )
    | Read  (x)      -> [false, env, singletonBuffer (READ) <+ ST (x)]
    | Write (e)      -> (
        case env.genLabel of
            [lab, env] -> case compile (lab, env, e) of
                [labUsed, env, code] -> [false, env, code <+> label (lab, labUsed) <+ WRITE]
            esac
        esac
    )
    | Assn  (x, e)   -> (
        case env.genLabels(2) of
            [lab1, lab2, env] -> case compile (lab1, env, x) of
                [lab1Used, env, code1] -> case compile (lab2, env, e) of
                    [lab2Used, env, code2] -> [false, env, code1 <+> label (lab1, lab1Used) <+> code2 <+> label (lab2, lab2Used) <+ STI]
                esac
            esac
        esac
    )
    | Seq   (e1, e2) -> (
        case env.genLabel of
          [e2Lab, env] ->
             case compile (e2Lab, env, e1) of
               [e2LabUsed, env, e1Code] ->
                  case compile (lab, env, e2) of
                    [labUsed, env, e2Code] -> [labUsed, env, e1Code <+> label (e2Lab, e2LabUsed) <+> e2Code]
                  esac
             esac
        esac
    )
    | If (cond, e1, e2) -> (
        case env.genLabels(2) of
          [e2Lab, condLab, env] ->
             case compile (lab, env, e1) of
                [_, env, e1Code] ->
                  case compile (lab, env, e2) of
                    [_, env, e2Code] -> case compile(condLab, env, cond) of
                        [condLabUsed, env, condCode] -> [
                            true,
                            env,
                            condCode <+> label (condLab, condLabUsed) <+> singletonBuffer(CJMP("z", e2Lab)) <+> e1Code <+> singletonBuffer(JMP(lab)) <+> singletonBuffer(LABEL(e2Lab)) <+> e2Code
                        ]
                    esac
                  esac
             esac
        esac
    )
    | While (cond, body) ->
        case env.genLabels(2) of
          [sLabel, condLabel, env] ->
            case compile (sLabel, env, body) of
              [_, env, bodyCode] -> case compile (condLabel, env, cond) of
                  [condLabelUsed, env, condCode] -> [
                      true,
                      env,
                      singletonBuffer(LABEL(sLabel)) <+> condCode <+> label (condLabel, condLabelUsed) <+> singletonBuffer(CJMP("z", lab)) <+> bodyCode <+> singletonBuffer(JMP(sLabel))
                  ]
              esac
            esac
        esac
    | DoWhile (body, cond) -> compile (lab, env, Seq (body, While (cond, body)))
    esac
  }  

  case initCompEnv ().genLabel of
    [endLab, env] ->
       case compile (endLab, env, stmt) of
         [endLabUsed, _, code] -> getBuffer $  code <+> label (endLab, endLabUsed)
       esac
  esac
}
